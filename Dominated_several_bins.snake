SAMPLES = ["sample"+str(i) for i in range(1,12)]
DIR_WITH_SAMPLES = "/Sid/snurk/mts/data/infant_gut/"
NAME_OF_LEFT_READ = "left.fastq.gz"
NAME_OF_RIGHT_READ = "right.fastq.gz"

DIR_WITH_BINS = "/Sid/ygorshkov/mts/multi-ig/main_concoct/reassembly/"

BINS = ["BIN%s" % i for i in [10, 28]]

#PATH_TO_CONTIGS = "/Sid/ygorshkov/mts/multi-ig/main_concoct/reassembly/BIN28/contigs.fasta"

PATH_TO_SCRIPTS = "~/meta-strains/scripts"
CONTIGS_MIN_LEN = 10000


#_____________________________________________________________________________


rule all:
    input:
        expand("{bin}/hists/{sample}.png", bin=BINS, sample=SAMPLES)


rule cp_long_contigs:
    input:
         DIR_WITH_BINS + "{bin}/contigs.fasta"
    output:
        "{bin}/contigs/contigs.fasta"
    log:
        "logs/cp_long_contigs/{bin}.log"
    shell:
        "(python {PATH_TO_SCRIPTS}/scripts_for_desman/cp_long_contigs.py {input} {output} {CONTIGS_MIN_LEN}) 2> {log}"


rule join_contigs:
    input:
        "{bin}/contigs/contigs.fasta"
    output:
        "{bin}/contigs/joined_contigs.fasta"
    shell:
        "python ~/meta-strains/scripts/join_contigs.py 100 {input} {wildcards.bin}_joined_long_contigs {output}"


rule concat_contigs:
    input:
        expand("{bin}/contigs/joined_contigs.fasta", bin=BINS)
    output:
        "concat_contigs/contigs.fasta"
    shell:
        "cat {input} > {output}"


rule bowtie2_index:
    input:
        "concat_contigs/contigs.fasta"
    output:
        "concat_contigs/contigs.fasta.1.bt2",
        "concat_contigs/contigs.fasta.2.bt2",
        "concat_contigs/contigs.fasta.3.bt2",
        "concat_contigs/contigs.fasta.4.bt2",
        "concat_contigs/contigs.fasta.rev.1.bt2",
        "concat_contigs/contigs.fasta.rev.2.bt2"
    log:
        "logs/bowtie2_index.log"
    shell:
        "(bowtie2-build {input} concat_contigs/contigs.fasta) 2> {log}"


rule bowtie2_map:
    input:
        contigs = "concat_contigs/contigs.fasta",
        index = lambda wildcards: expand("concat_contigs/contigs.fasta.{ext}",
                ext=["1.bt2", "2.bt2", "3.bt2", "4.bt2", "rev.1.bt2", "rev.2.bt2"]),
        R1 = DIR_WITH_SAMPLES + "{sample}/" + NAME_OF_LEFT_READ,
        R2 = DIR_WITH_SAMPLES + "{sample}/" + NAME_OF_RIGHT_READ
    output:
        "concat_contigs/mapped_reads/{sample}.bam"
    log:
        "logs/bowtie2_map/{sample}.log"
    shell:
        "(bowtie2 -p 16 -x {input.contigs} -1 {input.R1} -2 {input.R2} --maxins 1000 --n-ceil 0,0 --np 100500 " 
        " | samtools view -bS - > {output}) 2> {log}"


rule split_bam:
    input:
        "concat_contigs/mapped_reads/{sample}.bam"
    output:
        expand("concat_contigs/mapped_reads/{{sample}}.REF_{bin}_joined_long_contigs.bam", bin=BINS)    
    shell:
        "bamtools split -in {input} -reference"


rule cp_splited_bam:
    input:
        "concat_contigs/mapped_reads/{sample}.REF_{bin}_joined_long_contigs.bam"
    output:
        "{bin}/mapped_reads/{sample}.bam"
    shell:
        "cp {input} {output}"


rule samtools_sort:
    input:
        "{bin}/mapped_reads/{sample}.bam"
    output:
        "{bin}/sorted_reads/{sample}.bam"
    log:
        "logs/samtools_sort/{bin}/{sample}.log"
    shell:
        "samtools sort -T {wildcards.bin}/sorted_reads/{wildcards.sample} "
        "-O bam {input} > {output} 2> {log}"


rule samtools_mpileup:
    input:
        fa = "concat_contigs/contigs.fasta",
        bam = expand("{{bin}}/sorted_reads/{sample}.bam", sample=SAMPLES)
    output:
        "{bin}/calls/all.pileup"
    log:
        "logs/{bin}/samtools_mpileup.log"
    shell:
        "samtools mpileup -B -f {input.fa} {input.bam} > {output} 2> {log}"


rule varscan:
    input:
        "{bin}/calls/all.pileup"
    output:
        "{bin}/calls/all.varscan"
    log:
        "logs/{bin}/varscan.log"
    shell:
        "java -jar /home/makseshina/tools/VarScan.v2.3.9.jar mpileup2snp {input} "
        "--min-coverage	10 --min-reads2 5 --min-var-freq 0.02 > {output} 2> {log}"


rule analyze_dominated:
    input:
        "{bin}/calls/all.varscan"
    output:
        expand("{{bin}}/hists/{sample}.png", sample=SAMPLES),
        "{bin}/dominated_genotypes.png"
    log:
        "{bin}/dominated_analysis.txt"
    params:
        samples_list = ','.join(SAMPLES)
    shell:
        """
        (
        cd {wildcards.bin}
        python {PATH_TO_SCRIPTS}/dominated.py calls/all.varscan {params.samples_list} > dominated_analysis.txt
        )
        """
